apply plugin: "java"
apply plugin: "idea"
apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'

//apply plugin: "application"

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.0.0' // cobertura plugin
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report


//======================================================================
// CONFIGURATIONS
//======================================================================

sourceCompatibility = 1.7
targetCompatibility = 1.7

idea {
    project {
        jdkName "1.7"
        languageLevel "1.7"
    }
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir

    }
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

test {
    testLogging {
        events 'started', 'passed'
    }
}

//======================================================================
// TASKS
//======================================================================

task cucumber(type: JavaExec) {
    dependsOn assemble, compileTestJava
    main = "cucumber.api.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = ['-f', 'pretty', '--glue', 'behaviors', 'src/test/resources']
}

task createWrapper(type: Wrapper) {
    gradleVersion = 2.4
}

//======================================================================
// DEPENDENCIES
//======================================================================

dependencies {
    // UNIT TESTING
    testCompile group: "junit", name: "junit", version: "4.12"
    testCompile group: "org.easymock", name: "easymock", version: "3.3.1"

    // CUCUMBER
    testCompile group: "info.cukes", name: "cucumber-junit", version: "1.1.8"
    testCompile group: "info.cukes", name: "cucumber-java", version: "1.1.8"

    // LOGGING
    compile group: "org.slf4j", name: "slf4j-api", version: "1.7.5"
    compile  group: "org.slf4j", name: "jcl-over-slf4j", version: "1.7.5"
    compile  group: "org.slf4j", name: "log4j-over-slf4j", version: "1.7.5"
    compile  group: "ch.qos.logback", name: "logback-core", version: "1.1.2"
    compile  group: "ch.qos.logback", name: "logback-classic", version: "1.1.2"
}

